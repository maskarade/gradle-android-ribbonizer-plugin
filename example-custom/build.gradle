import com.android.build.gradle.api.ApplicationVariant

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.github.gfx.ribbonizer'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug {}
        beta {
            debuggable true
        }
        release {}
    }

    productFlavors {
        local {}
        staging {}
        production {}
    }
}

ribbonizer {
    iconNames "@drawable/abc_btn_check_to_on_mtrl_000", "@drawable/abc_btn_radio_to_on_mtrl_000"

    builder { ApplicationVariant variant, File iconFile ->
        if (variant.flavorName == "local") {
            return grayRibbonFilter(variant, iconFile)
        } else if (variant.flavorName == "staging") {
            return yellowRibbonFilter(variant, iconFile)
        } else {
            return greenRibbonFilter(variant, iconFile)
        }
    }
}

dependencies {
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.android.support:appcompat-v7:23.1.1'
}
