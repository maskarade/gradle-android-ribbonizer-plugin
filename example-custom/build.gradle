import com.android.build.gradle.api.ApplicationVariant

apply plugin: 'com.android.application'
apply plugin: 'com.github.gfx.ribbonizer'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.0'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {}
        beta {
            debuggable true
        }
        canary {
            debuggable false
        }
        release {}
    }

    productFlavors {
        local {}
        qa {}
        staging {}
        production {}
    }
}

ribbonizer {
    iconNames "@drawable/abc_btn_check_to_on_mtrl_000", "@drawable/abc_btn_radio_to_on_mtrl_000", "@mipmap/ic_launcher_foreground"

    builder { ApplicationVariant variant, File iconFile ->
        if (variant.flavorName == "local") {
            return grayRibbonFilter(variant, iconFile)
        } else if (variant.flavorName == "qa") {
            def filter = customColorRibbonFilter(variant, iconFile, "#00C89C")
            filter.label = null//"QA" + variant.versionCode
            filter.largeRibbon = (iconFile.name == "ic_launcher_foreground.png")
            return filter
        } else if (variant.buildType.name == "debug") {
            if (variant.flavorName == "production") {
                return null
            }
            return customColorRibbonFilter(variant, iconFile, "#0000FF")
        } else {
            return greenRibbonFilter(variant, iconFile)
        }
    }

    forcedVariantsNames "localCanary"
}

dependencies {
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.android.support:appcompat-v7:25.0.0'
}
