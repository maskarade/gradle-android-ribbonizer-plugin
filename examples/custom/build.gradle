plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.shogo82148.ribbonizer'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.shogo82148.ribbonizer.example"
        minSdkVersion 30
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildTypes {
        debug {}
        beta { debuggable true }
        canary { debuggable true }
        release {}
    }

    flavorDimensions 'flavor'
    productFlavors {
        local {
            dimension 'flavor'
        }
        qa {
            dimension 'flavor'
        }
        staging {
            dimension 'flavor'
        }
        production {
            dimension 'flavor'
        }
    }
}

ribbonizer {
    iconNames "@drawable/dog", "@drawable/thinking"
    forcedVariantsNames "localCanary"
    builder { variant, iconFile ->
        if (variant.flavorName == "local") {
            return grayRibbonFilter(variant, iconFile)
        } else if (variant.flavorName == "qa") {
            def filter = customColorRibbonFilter(variant, iconFile, "#00C89C")
            filter.label = "QA" + variant.versionCode
            filter.largeRibbon = (iconFile.name == "ic_launcher.png")
            return filter
        } else if (variant.buildType.name == "debug") {
            if (variant.flavorName == "production") {
                return null
            }
            return customColorRibbonFilter(variant, iconFile, "#0000FF")
        } else {
            return greenRibbonFilter(variant, iconFile)
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
}